# Remix Fund Me
Let us make a remix fund me using the REMIX IDE. The fund me contract will allow the people to send funds to owner of the contract contract.

We will set a minimum amount of funds that can be sent to the contract, let us say `50$` in ethereum, if the amount sent is less than `50$` then the contract will reject the transaction.

Then we will make so that the owner of the contract can withdraw the funds to his wallet.

We will also be able to check the funders and see how much have they sent to the contract.

![final](https://i.imgur.com/gULjT4l.png)

# Using Libraries

We can make functions in our contract but that will make our contract very large and hard to read.

So we will move those functions to a library and then import that library in our contract. Then we can use those functions as a method on our variables.

Whenever we add a function in a library we have to make it `internal`

```solidity
//SPDX-License-Identifier: MIT
pragma solidity ^0.8.8;

import "@chainlink/contracts/src/v0.8/interfaces/AggregatorV3Interface.sol";
  
    // Address - 0x694AA1769357215DE4FAC081bf1f309aDC325306
library PriceConverter{
    function getPrice() internal view returns(uint256) {
        AggregatorV3Interface priceFeed = AggregatorV3Interface(0x694AA1769357215DE4FAC081bf1f309aDC325306);
        (,int256 price,,,) = priceFeed.latestRoundData(); //8 decimal
        return uint256(price * 1e10); // 18 decimal for wei
    }

    function getConversionRate (uint256 ethAmount) internal view returns(uint256) {
        uint256 ethPrice = getPrice();
        uint256 ethAmountInUSD = (ethPrice * ethAmount) / 1e18;
        return ethAmountInUSD;

    }

    function getVersion() internal view returns (uint256) {
        AggregatorV3Interface priceFeed = AggregatorV3Interface(0x694AA1769357215DE4FAC081bf1f309aDC325306);
        return priceFeed.version();
    }
}
```

Let us take a look at the functions now.

# Getting the current price

The blockchain cannot interact with the internet directly, so we will use an oracle which will tell us the price of the ethereum in USD. We will use the `Chainlink` oracle for this. Chainlink data feeds allow smart contracts to access off-chain data securely and reliably.

`ABI` is the interface that tells us how to interact with the contract.

We will use the `AggregatorV3Interface` interface to get the price of the ethereum in USD. We import the interface fromt the `chainlink` npm library.
[Chainlink Library](https://www.npmjs.com/package/@chainlink/contracts)

We have different addresses for different pairs, we will use the `ETH/USD` pair for the `sepolia` network.

The required address is `0x694AA1769357215DE4FAC081bf1f309aDC325306`

![address](https://i.imgur.com/DIR0Rhb.png)
[Chainlink Data Feeds](https://docs.chain.link/data-feeds/price-feeds/addresses?network=ethereum&page=1)

We will use the `latestRoundData` function to get the latest price of the ethereum in USD.

```solidity
function getPrice() internal view returns(uint256) {
        AggregatorV3Interface priceFeed = AggregatorV3Interface(0x694AA1769357215DE4FAC081bf1f309aDC325306);
        (,int256 price,,,) = priceFeed.latestRoundData(); //8 decimal
        return uint256(price * 1e10); // 18 decimal for wei
    }
```

The `latestRoundData` function returns a tuple, we destructure the `price` variable from the tuple. Here destructuring is a bit different, we have to add commas for the variables that we do not want to destructure.

# Understanding decimals in solidity

Solidity does not support decimals.

1 ETH = 10^18 wei which is represented as `1e18` in solidity. All calculations are done in wei.

So basically we need to make everything in wei, that is 18 decimals long.
In the above function we get the price in 8 decimals, so we multiply it by `1e10` to make it 18 decimals long.

# Getting the conversion rate

We get the conversion rate by multiplying the price of the ethereum in USD with the amount of ethereum sent to the contract.

```solidity
    function getConversionRate (uint256 ethAmount) internal view returns(uint256) {
        uint256 ethPrice = getPrice();
        uint256 ethAmountInUSD = (ethPrice * ethAmount) / 1e18;
        return ethAmountInUSD;
    }
```

`Note: Always multiply first and then divide, otherwise you will lose precision`

Here after multiplying we get a total of 36 decimals, so we divide it by `1e18` to make it 18 decimals long.

## Using the library in contract

First we import the library in our contract and add `using PriceConverter for uint256;` to use the library on our variables.

We set the minimumUSD to `50$` and multiply it by `1e18` to make it 18 decimals long.

```solidity
uint256 public minUSD = 50 * 1e18; 
```

We create an array of addresses to store the funders. And a mapping to store the amount of funds sent by each funder.

```solidity
address[] public funders;
    mapping(address => uint256) public addressToAmountFunded;

    function fund() public payable{
        require(msg.value.getConversionRate() >= minUSD, "Did not send enough!");
        //if less reverts
        funders.push(msg.sender);
        addressToAmountFunded[msg.sender] = msg.value;
    }
```

  - **Payable:** We need to make a function payable whenever we want to send funds to the contract.

  - **Require:** We use require to check if the amount sent is greater than the minimum amount. If it is not then the transaction will be reverted. This saves gas as the transaction will not be executed. Then also some amount of gas will be used to revert the transaction.

# Withdraw funds
We want to make a function that will allow the owner of the contract to withdraw the funds to his wallet. 

To make a person owener of the contract we will use the `constructor` function. The constructor function is called only once when the contract is deployed, before executing any other function.

```solidity
    address public owner;

    constructor() {
        owner = msg.sender;
    }
```

This will set the owner of the contract to the person who deployed the contract, and he will only be able to withdraw the funds.

First of all we reset the mapping using for loop and array to empty.  

```solidity
for(uint256 idx=0; idx<funders.length; idx++){
            address funder = funders[idx];
            addressToAmountFunded[funder] = 0;  
        }

        //reset array
        funders = new address[](0);
```

we set funders to a new address array containing 0 elements.

Then we transfer the funds to the owner of the contract. The funds of the contract can be accessed by `address(this).balance`

We can withdraw funds using `transfer`, `send` or `call` methods.
You can read more about it here:

![withdraw](https://i.imgur.com/sIpsEvI.png)
[Transer, Send & Call methods](https://solidity-by-example.org/sending-ether/)

In summary, we use `call` method as it is recommended.

To do that we need to make the `msg.sender` `payable`. Then we use the `call` method to get the value of the funds in the contract.

```solidity
(bool callSuccess, ) = payable(msg.sender).call{value: address(this).balance}("");
        require(callSuccess, "Call failed");
```

We need the returned value to be `true` that is transaction is successfull, otherwise we will revert the transaction.

# Modifiers

We can use modifiers to modify the functions and to add some checks before executing the function.

We can make a modifier by using the `modifier` keyword.

We can add stuff to the modifier. The `_` is used to tell the compiler where to execute the parent function. That is either before or after the modifier.

Here we will use a modifier to check if the person calling the function is the owner of the contract.

```solidity
    modifier onlyOwner {
        require(msg.sender == owner, "Sender is not owner!");
        _;
    }
```

This says us that first execute the modifier and then execute the function.

We can use the modifier by adding `onlyOwner` after the function name.

```solidity
function withdraw() public onlyOwner {
    //code
}
```

By using this can call the modifier before executing a owner only function.

# Etherscan Links

Here is the link to the contract page which now holds some ethereum.

[Fund Me Contract](https://sepolia.etherscan.io/address/0x813cb854e9e6b5562531be6d8671ad8b4a2695ca)